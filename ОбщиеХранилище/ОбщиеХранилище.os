#Использовать ОбщиеСистема
#Использовать ОбщиеСтрока
#Использовать ОбщиеОтладка

Функция ХранилищеПуть(ХранилищеКаталог) Экспорт
	Возврат ХранилищеКаталог + "\1cv8ddb.1CD";
КонецФункции

Процедура ХранилищеКомандаВыполнить(ХранилищеКаталог, Команда) Экспорт
	Tool_1CD = ОбщиеСистема.КаталогПуть(ТекущийСценарий().Каталог, "bin\cTool_1CD.exe");
	Лог = ПолучитьИмяВременногоФайла("log");
	Попытка
		ОбщиеСистема.КомандаВыполнить(СтрЗаменить(СтрЗаменить(СтрЗаменить(СтрЗаменить("%Tool_1CD% -q -ne %ХралилищеКаталог% %Команда% -l %Лог%"
		, "%Tool_1CD%", ОбщиеСтрока.Обрамить(Tool_1CD))
		, "%ХралилищеКаталог%", ОбщиеСтрока.Обрамить(ХранилищеПуть(ХранилищеКаталог))), "%Команда%", Команда), "%Лог%", ОбщиеСтрока.Обрамить(Лог)));
	Исключение
		Информация = ИнформацияОбОшибке();
	КонецПопытки;
	ЛогФайл = Новый Файл(Лог);
	Если ЛогФайл.Существует() Тогда
		Чтение = Новый ЧтениеТекста(Лог);
		ЛогТекст = Символы.ПС + "Лог:" + Символы.ПС + Чтение.Прочитать();
		Чтение.Закрыть();
		УдалитьФайлы(Лог);
	КонецЕсли;
	Если Информация <> Неопределено Тогда
		ВызватьИсключение Информация.Описание + ЛогТекст;
	КонецЕсли;
КонецПроцедуры

Функция ХранилищеТаблицы(ХранилищеКаталог, ТаблицыИмена, КаталогВременный) Экспорт
	ХранилищеКомандаВыполнить(ХранилищеКаталог, СтрЗаменить(СтрЗаменить("-ex %Каталог% %Список%"
	, "%Каталог%", ОбщиеСтрока.Обрамить(КаталогВременный))
	, "%Список%", ОбщиеСтрока.Обрамить(ТаблицыИмена)));
	Таблицы = Новый Структура;
	Для Каждого ТаблицаИмя Из ОбщиеСтрока.СтрокаМассив(ТаблицыИмена, ";") Цикл
		ЧтениеXML = Новый ЧтениеXML;
		ЧтениеXML.ОткрытьФайл(ОбщиеСистема.КаталогПуть(КаталогВременный, ТаблицаИмя + ".xml"));
		ЧтениеXML.ПерейтиКСодержимому();
		ЧтениеXML.Прочитать();
		Таблица = Новый ТаблицаЗначений;
		КолонкиТипы = Новый Соответствие;
		Пока ЧтениеXML.Прочитать() И НЕ (ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента И ЧтениеXML.Имя = "Fields") Цикл
			Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента И ЧтениеXML.Имя = "Field" Тогда
				КолонкаИмя = ЧтениеXML.ЗначениеАтрибута("Name");
				Таблица.Колонки.Добавить(КолонкаИмя);
				КолонкиТипы.Вставить(КолонкаИмя, ЧтениеXML.ЗначениеАтрибута("Type"));
			КонецЕсли;
		КонецЦикла;
		Пока ЧтениеXML.Прочитать() Цикл 
			Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента И ЧтениеXML.Имя = "Record" Тогда
				Строка = Таблица.Добавить();
				Пока ЧтениеXML.Прочитать() И НЕ (ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента И ЧтениеXML.Имя = "Record") Цикл
					Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда					
						Значение = "";
						Пока ЧтениеXML.Прочитать() И ЧтениеXML.ТипУзла <> ТипУзлаXML.КонецЭлемента Цикл							
							Если ЧтениеXML.ТипУзла = ТипУзлаXML.Текст Тогда
								Значение = ЧтениеXML.Значение;
							КонецЕсли;
						КонецЦикла;
						КолонкаИмя = ЧтениеXML.Имя;
						КолонкаТип = КолонкиТипы[КолонкаИмя];
						Если КолонкаТип = "number" Тогда
							Значение = Число(Значение);
						ИначеЕсли КолонкаТип = "bool" Тогда
							Значение = Булево(Значение);
						ИначеЕсли КолонкаТип = "datetime" Тогда
							Значение = ОбщиеСтрока.СтрокаДата(Значение);
						КонецЕсли;
						Строка[КолонкаИмя] = Значение;
					КонецЕсли;
				КонецЦикла;	
			КонецЕсли;
		КонецЦикла;
		ЧтениеXML.Закрыть();
		Таблицы.Вставить(ТаблицаИмя, Таблица);
	КонецЦикла;
	Возврат Таблицы;
КонецФункции

Функция ХранилищеВерсииАвторы(ХранилищеКаталог, Знач КаталогВременный = Неопределено) Экспорт
	КаталогВременныйСоздавать = КаталогВременный <> Неопределено;
	Если КаталогВременныйСоздавать Тогда
		КаталогВременный = ПолучитьИмяВременногоФайла();
		СоздатьКаталог(КаталогВременный);
	КонецЕсли;
	Таблицы = ХранилищеТаблицы(ХранилищеКаталог, "VERSIONS;USERS", КаталогВременный);
	Пользователи = Новый Соответствие;
	Для Каждого Пользователь Из Таблицы.USERS Цикл
		Пользователи.Вставить(Пользователь.USERID, Пользователь.NAME);
	КонецЦикла;
	ВерсииАвторы = Новый ТаблицаЗначений;
	КолонкиИмена = "Номер, Дата, Комментарий, Автор";
	Для Каждого Колонка Из ОбщиеСтрока.СтрокаМассив(КолонкиИмена, ",") Цикл
		ВерсииАвторы.Колонки.Добавить(Колонка);
	КонецЦикла;
	Для Каждого Версия Из Таблицы.VERSIONS Цикл
		Строка = ВерсииАвторы.Добавить();
		Строка.Номер = Версия.VERNUM;
		Строка.Дата = Версия.VERDATE;
		Строка.Комментарий = Версия.COMMENT;
		Строка.Автор = Пользователи[Версия.USERID];
	КонецЦикла;
	ВерсииАвторы.Сортировать("Номер");
	Если КаталогВременныйСоздавать Тогда
		УдалитьФайлы(КаталогВременный);
	КонецЕсли;
	Возврат ВерсииАвторы;
КонецФункции 
